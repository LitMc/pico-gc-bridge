; 本体役としてJoyBusの送受信を行う
; サイクルの周波数は4MHz
; 1ビットにかける時間は送信5us、受信4us
.program joybus_console

.wrap_target
done:
    irq set 0 rel

public rx_start:
    wait 1 pin 0                            ; アイドルHigh待ち
    wait 0 pin 0                            ; cycle0 Low待ち（立ち下がり）
                                            ; Lowが1usより長く続いたら'0'
fall_edge:
    set x, 1                                ; cycle1
    set y, 1                                ; cycle2
low:
    jmp pin high                            ; 3 + 2x, 7 + 2x + 2k, 13
    jmp x-- low                             ; 4 + 2x
zero_detected:
    set y, 0                                ; 5 + 2x
    jmp low                                 ; 6 + 2x
high:
    in y, 1                                 ; 4 + 2x, 8 + 2x + 2k, 14
    set x, 8                                ; 5 + 2x, 9 + 2x + 2k, 15
wait_low:
    jmp pin wait_timeout                    ; 6 + 2x + 2x', 10 + 2x + 2k, 16 + 2x'
    jmp fall_edge
wait_timeout:
    jmp x-- wait_low                        ; 7 + 2x + 2x', 11 + 2x + 2k + 2x', 17 + 2x'
timeout:
    push noblock
    jmp done

public tx_start:
    out x, 32                               ; 0
tx_bit_loop:
    set pindirs, 0                          ; 1
    jmp !x tx_stop [3]                      ; 2から5
    out pindirs, 1 [9]                      ; 6から15
    set pindirs, 1 [3]                      ; 16から19
    jmp x-- tx_bit_loop                     ; 20
tx_stop:
    set pindirs, 1                          ; 6から
.wrap
